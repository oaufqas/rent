terraform {
  required_providers {
    yandex = {
      source = "yandex-cloud/yandex"
    }
    helm = {
      source = "hashicorp/helm"
    }
  }
}

provider "yandex" {
  token     = var.token
  cloud_id  = var.cloud_id
  folder_id = var.folder_id
  zone      = var.zone
}

provider "helm" {
  kubernetes = {
    host                   = yandex_kubernetes_cluster.k8s-cluster.master[0].external_v4_address
    cluster_ca_certificate = yandex_kubernetes_cluster.k8s-cluster.master[0].cluster_ca_certificate

    token = var.token
  }
}



resource "yandex_vpc_network" "k8s-network" {
  name = "k8s-network"
}

resource "yandex_vpc_address" "ingress-static-ip" {
  name = "ingress-ip"
  external_ipv4_address {
    zone_id = var.zone
  }
}

resource "yandex_vpc_subnet" "k8s-subnet" {
  name           = "k8s-subnet"
  network_id     = yandex_vpc_network.k8s-network.id
  v4_cidr_blocks = ["192.168.10.0/24"]
  zone           = var.zone
}

resource "yandex_iam_service_account" "k8s-sa" {
  name        = "k8s-account"
  description = "Service account for Kubernetes cluster"
}

resource "yandex_resourcemanager_folder_iam_member" "k8s-roles" {
  for_each  = toset(["editor", "container-registry.images.puller"])
  folder_id = var.folder_id
  role      = each.key
  member    = "serviceAccount:${yandex_iam_service_account.k8s-sa.id}"
}

resource "yandex_kubernetes_cluster" "k8s-cluster" {
  name       = "k8s-cluster"
  network_id = yandex_vpc_network.k8s-network.id

  master {
    zonal {
      zone      = yandex_vpc_subnet.k8s-subnet.zone
      subnet_id = yandex_vpc_subnet.k8s-subnet.id
    }
    public_ip = true
  }

  service_account_id      = yandex_iam_service_account.k8s-sa.id
  node_service_account_id = yandex_iam_service_account.k8s-sa.id

  depends_on = [
    yandex_resourcemanager_folder_iam_member.k8s-roles
  ]
}

resource "yandex_kubernetes_node_group" "nodes" {
  cluster_id = yandex_kubernetes_cluster.k8s-cluster.id
  name       = "nodes"

  instance_template {
    platform_id = "standard-v3"

    resources {
      memory = 4
      cores  = 2
    }

    boot_disk {
      type = "network-hdd"
      size = 32
    }

    scheduling_policy {
      preemptible = true
    }

    network_interface {
      nat        = true
      subnet_ids = [yandex_vpc_subnet.k8s-subnet.id]
    }
  }

  scale_policy {
    fixed_scale {
      size = 2
    }
  }

  allocation_policy {
    location {
      zone = var.zone
    }
  }
}

resource "helm_release" "ingress-nginx" {
  name             = "ingress-nginx"
  repository       = "https://kubernetes.github.io/ingress-nginx"
  chart            = "ingress-nginx"
  namespace        = "ingress-nginx"
  create_namespace = true

  set = [
    {
      name  = "controller.service.loadBalancerIP"
      value = yandex_vpc_address.ingress-static-ip.external_ipv4_address[0].address
    }
  ]
  depends_on = [
    helm_release.cert_manager
  ]
}

resource "helm_release" "cert_manager" {
  name             = "cert-manager"
  repository       = "oci://quay.io/jetstack/charts"
  chart            = "cert-manager"
  namespace        = "cert-manager"
  create_namespace = true
  version          = "v1.16.1"

  set = [
    {
      name  = "installCRDs"
      value = "true"
    }
  ]
  depends_on = [
    yandex_kubernetes_cluster.k8s-cluster
  ]
}

resource "helm_release" "nfs_server" {
  name             = "nfs-server-provisioner"
  repository       = "https://kubernetes-sigs.github.io/nfs-ganesha-server-and-external-provisioner"
  chart            = "nfs-server-provisioner"
  namespace        = "nfs-system"
  create_namespace = true

  set = [{
    name  = "persistence.enabled"
    value = "true"
    },
    {
      name  = "persistence.size"
      value = "4Gi"
    },
    {
      name  = "persistence.storageClass"
      value = "yc-network-hdd"
    },
    {
      name  = "storageClass.name"
      value = "nfs"
  }]
}

resource "helm_release" "vault" {
  name             = "vault"
  repository       = "oci://cr.yandex/yc-marketplace/yandex-cloud/vault/chart"
  chart            = "vault"
  version          = "0.28.1+yckms"
  namespace        = "vault"
  create_namespace = true

  values = [
    yamlencode({
      server = {
        dev = { enabled = false }

        ha = {
          enabled  = true
          replicas = 1
          raft = {
            enabled   = true
            setNodeId = true
            config    = <<EOT
              ui = true
              listener "tcp" {
                tls_disable = 1
                address = "[::]:8200"
                cluster_address = "[::]:8201"
              }
              storage "raft" {
                path = "/vault/data"
              }
            EOT
          }
        }

        image = {
          repository = "mirror.gcr.io/hashicorp/vault"
          tag        = "1.15.0"
        }

        dataStorage = {
          enabled = true
          size    = "5Gi"
        }
      }
    })
  ]
}

resource "helm_release" "external_secrets" {
  name             = "external-secrets"
  repository       = "https://external-secrets.io"
  chart            = "external-secrets"
  namespace        = "external-secrets"
  create_namespace = true

  set = [{
    name  = "installCRDs"
    value = "true"
  }]

  depends_on = [helm_release.vault]
}

output "ingress_external_ip" {
  value = yandex_vpc_address.ingress-static-ip.external_ipv4_address[0].address
}
